PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

# Flush KBO identifiers (from identifier-kbo.rq)
DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bestuurseenheid
      ext:kbonummer ?kboNummer ;
      dcterms:identifier ?kboNummer .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/landing-zone/organizations-public-info> {
    ?identifier
      skos:notation "KBO nummer" ;
      generiek:gestructureerdeIdentificator ?gestructureerdeIdentificator .

    ?gestructureerdeIdentificator
      generiek:lokaleIdentificator ?kboNummer .

    ?bestuurseenheid
      adms:identifier ?identifier .
  }
};

# Flush OVO identifiers (from identifier-ovo.rq)
DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bestuurseenheid
      dcterms:identifier ?ovoNummer .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/landing-zone/organizations-public-info> {
    ?identifier
      skos:notation "OVO-nummer" ;
      generiek:gestructureerdeIdentificator ?gestructureerdeIdentificator .

    ?gestructureerdeIdentificator
      generiek:lokaleIdentificator ?ovoNummer .

    ?bestuurseenheid
      adms:identifier ?identifier .
  }
}
