PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX adres: <https://data.vlaanderen.be/ns/adres#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?postInfo a adres:Postinfo ;
              ?p ?o .
  }
}
INSERT {
  GRAPH ?g {
    ?postInfo a adres:Postinfo ;
              ?p ?o .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?postInfo a adres:Postinfo ;
              ?p ?o ;
              geo:sfWithin ?werkingsgebied .
  }
  ?bestuurseenheid besluit:werkingsgebied ?werkingsgebied ;
                   mu:uuid ?adminUnitUuid .
  BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?adminUnitUuid)) AS ?g)
}
