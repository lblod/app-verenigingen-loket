PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX mu:<http://mu.semte.ch/vocabularies/core/> 
PREFIX ext:<http://mu.semte.ch/vocabularies/ext/> 
PREFIX org: <http://www.w3.org/ns/org#> 
PREFIX geo: <http://www.opengis.net/ont/geosparql#> 
PREFIX adres: <https://data.vlaanderen.be/ns/adres#> 
PREFIX organisatie: <https://data.vlaanderen.be/ns/organisatie#> 
PREFIX locn: <http://www.w3.org/ns/locn#> 



DELETE {
  GRAPH  <http://mu.semte.ch/graphs/public> {
             ?association
                        ?x ?y .
  }
} INSERT {
        GRAPH ?g {
                ?association
                        ?x ?y .
        }
}
WHERE {
        ?bestuurseenheid
                mu:uuid ?adminUnitUuid;
                org:classification/skos:prefLabel "Gemeente" ;
                besluit:werkingsgebied ?werkingsgebied .

        ?werkingsgebied
                ?p ?o .

        ?postInfo
                geo:sfWithin ?werkingsgebied;
                ?p2 ?o2 ;
                adres:postcode ?code ;
                adres:postnaam ?name .

        ?association 
                a <https://data.vlaanderen.be/ns/FeitelijkeVerenigingen#FeitelijkeVereniging> , org:Organization ;
                ?x ?y ; 
                org:hasPrimarySite ?primarySite .

        ?primarySite 
                organisatie:bestaatUit ?address .

        ?address 
                locn:postCode ?code .
        

        BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?adminUnitUuid)) AS ?g)
}


